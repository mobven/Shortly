plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

apply from: "../jacoco.gradle"
apply from: "../sonarqube.gradle"
apply from: "../sdpssp.gradle"

android {

    compileSdk 33

    defaultConfig {
        applicationId "com.mobven.shortly"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "BASE_URL", "\"https://api.shrtco.de/v2/\""
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            manifestPlaceholders["crashlytics_enabled"] = "false"
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        release {
            minifyEnabled false
            manifestPlaceholders["crashlytics_enabled"] = "true"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    namespace 'com.mobven.shortly'
}

dependencies {
    implementation KotlinX.coroutines.android
    implementation AndroidX.core.ktx
    implementation AndroidX.appCompat
    implementation Google.android.material

    implementation Square.retrofit2
    implementation Square.retrofit2.converter.gson
    implementation 'com.google.code.gson:gson:_'
    implementation Square.okHttp3.loggingInterceptor

    implementation AndroidX.room.ktx
    implementation AndroidX.navigation.fragmentKtx
    implementation AndroidX.navigation.uiKtx
    kapt AndroidX.room.compiler

    implementation Google.dagger.hilt.android
    implementation AndroidX.hilt.navigationFragment
    kapt Google.dagger.hilt.compiler

    implementation 'com.github.mobven:extensify:_'

    implementation AndroidX.test.espresso.idlingResource
    implementation AndroidX.room.paging
    implementation AndroidX.paging.runtimeKtx

    implementation platform(Firebase.bom)
    implementation Firebase.analyticsKtx
    implementation Firebase.crashlyticsKtx

    testImplementation Testing.junit4
    testImplementation Testing.mockK
    testImplementation KotlinX.coroutines.test
    testImplementation AndroidX.archCore.testing
    testImplementation CashApp.turbine

    androidTestImplementation AndroidX.test.runner
    androidTestImplementation AndroidX.test.rules
    androidTestImplementation AndroidX.test.ext.junit
    androidTestImplementation AndroidX.test.espresso.core

    implementation 'com.google.android.play:review-ktx:_'

}